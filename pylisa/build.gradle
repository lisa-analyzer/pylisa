plugins {
    id 'java'
    id 'application'
   	id 'antlr'
   	id 'eclipse'
   	id "com.diffplug.spotless" version "6.18.0"
   	id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
    maven {
        name = 'GitHubPackages'
        url = 'https://maven.pkg.github.com/lisa-analyzer/lisa'
        credentials {
            username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
            password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
        }
    }
}

group = 'it.unive'
version = '0.1'
mainClassName = 'it.unive.pylisa.PyLiSA'

dependencies {
    antlr 'org.antlr:antlr4:4.8-1'
    
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'

    testCompileOnly 'org.projectlombok:lombok:1.18.22'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.22'
    
    implementation 'io.github.lisa-analyzer:lisa-sdk:0.1'
    implementation 'io.github.lisa-analyzer:lisa-program:0.1'
    implementation 'io.github.lisa-analyzer:lisa-analyses:0.1'

    implementation 'guru.nidi:graphviz-java:0.18.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    implementation 'org.apache.httpcomponents:httpclient:4.5.14'
    implementation 'org.springframework:spring-web:6.1.6'
    implementation 'org.jdom:jdom2:2.0.5'
    implementation 'com.sun.xml.bind:jaxb-impl:3.0.2'    
    implementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
    implementation 'com.google.code.gson:gson:2.8.7'
    implementation 'io.github.classgraph:classgraph:4.8.175'
    
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.thymeleaf:thymeleaf:3.0.15.RELEASE'
}

generateGrammarSource {
    dependsOn "sourcesJar"
    maxHeapSize = "64m"
    arguments += ["-visitor", "-no-listener"]
    
        doLast {
    	copy {
      		from 'build/generated-src/antlr/main/'
      		include '*.*'
      		into 'build/generated-src/antlr/main/it/unive/pylisa/antlr'
    	}
   		project.delete fileTree('build/generated-src/antlr/main').include('*.*')
  	}
}

java {
    withJavadocJar()
    withSourcesJar()
}

compileJava   {
  sourceCompatibility = '17'
  targetCompatibility = '17'
}

javadoc {
	if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

spotless {
    enforceCheck = false
    encoding 'UTF-8'
    lineEndings 'UNIX'
    java {
        // tabs, no spaces
        indentWithTabs()
    
        // keep imports clean 
        importOrder() 
        removeUnusedImports()
    
        // use the eclipse formatting with the one provided with the project
        eclipse().configFile(new File(rootProject.projectDir, 'spotless-formatting.xml'))
    
        target 'src/**/*.java'    
        // ignore generated code
        targetExclude '**/build/generated/**/*.java'
        targetExclude '**/build/generated-src/**/*.java'
        targetExclude '**/target/generated-sources/**/*.java'
    }
    antlr4 {
        target 'src/*/antlr/**/*.g4'
        antlr4Formatter() 
    }
}

spotlessJava {
	// declaring explicit dependencies
	dependsOn 'compileJava', 'compileTestJava', 'processTestResources', 'spotlessAntlr4'
}

spotlessJavaCheck { 
    finalizedBy 'spotlessErrorMessage'
}

spotlessAntlr4Check { 
    finalizedBy 'spotlessErrorMessage'
}

task spotlessErrorMessage {
    onlyIf {
        spotlessJavaCheck.state.failure != null || spotlessAntlr4Check.state.failure != null
    }
    doLast {
        logger.error('Spotless plugin thrown an error. This means that the code is not correctly formatted.')
        logger.error('To reproduce locally, execute \'./gradlew spotlessCheck\'')
        logger.error('To automatically fix all the problems, execute \'./gradlew spotlessApply\'')
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.name 
            groupId = 'io.github.lisa-analyzer'
            from components.java
            pom {
                name = 'PyLiSA (' + project.name + ')' 
                description = 'Python frontend for LiSA'
                url = 'https://lisa-analyzer.github.io/'
                packaging = 'jar'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'lucaneg'
                        name = 'Luca Negrini'
                        email = 'luca.negrini@unive.it'
                        url = 'https://lucaneg.github.io/'
                        organization = 'Ca\' Foscari University of Venice, Italy'
                        organizationUrl = 'https://www.unive.it/'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/lisa-analyzer/pylisa.git'
                    developerConnection = 'scm:git:ssh://github.com/lisa-analyzer/pylisa.git'
                    url = 'https://github.com/lisa-analyzer/pylisa'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = System.getenv('MAVEN_USERNAME')
                password = System.getenv('MAVEN_PASSWORD')
            }
        }
    }
}

signing {
    def signingKey = System.getenv('SIGN_KEY')
    def signingPassword = System.getenv('SIGN_PW')
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.mavenJava
}
